apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'sonar-runner'
apply plugin: 'application'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'
version = '0.1'

mainClassName = "controller.Main"

test {

    // explicitly include or exclude tests
    exclude 'view/**/*'

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message)
    }
}

sonarRunner {

    sonarProperties {
        property "sonar.host.url", "http://87.98.155.63:9000/"
        property "sonar.jdbc.url", "jdbc:mysql://localhost:3306/sonar"
        property "sonar.jdbc.username", "fidry_softQual"
        property "sonar.jdbc.password", "FmcxPbb3wbwbEFBZ"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.projectKey", "softwarequality"
        property "sonar.name", "Software Quality"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            srcDir 'src/test'
        }
    }
}

dependencies {
    compile 'com.jgoodies:jgoodies-binding:2.10.0',
            'com.jgoodies:jgoodies-common:1.7.0',
            'com.jgoodies:forms:1.2.1',
            'org.swinglabs:swingx:1.6.1'
    testCompile 'junit:junit:4.12-beta-3',
            'com.googlecode.multithreadedtc:multithreadedtc:1.01'
}
